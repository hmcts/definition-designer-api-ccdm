buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.0.1622"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'application'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '1.5.19.RELEASE'
    id 'org.owasp.dependencycheck' version '4.0.2'
    id 'se.patrikerdes.use-latest-versions' version '0.2.7'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.sonarqube' version '2.7'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.kt3k.coveralls" version "2.8.2"
    id "com.github.spotbugs" version "1.6.6"
    id 'io.franzbecker.gradle-lombok' version '2.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: "org.sonarqube"
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'se.patrikerdes.use-latest-versions'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'pmd'
apply from: 'liquibase.gradle'

mainClassName = 'uk.gov.hmcts.ccd.definition.designer.DefinitionDesignerApiApplication'

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 0
    suppressionFile = 'dependency-check-suppressions.xml'
    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

//overriding log4j2 default version 2.7 because of vulnerability issues
ext['log4j2.version'] = '2.11.2'
// end

ext {
    authCheckerVersion = '2.1.3'
    commonsLang3Version = '3.8.1'
    hamcrestVersion = 1.3
    hibernateValidatorVersion = '6.0.14.Final'
    junitJupiterVersion = '5.3.1'
    junitVintageVersion = '5.2.0'
    postgresqlVersion = '42.2.5'
    reformLogging= '3.0.2'
    springCloudVersion = 'Greenwich.RELEASE'
    springfoxSwaggerVersion = '2.9.2'
    limits = [
        'instruction': 99,
        'branch'     : 99,
        'line'       : 99,
        'complexity' : 89,
        'method'     : 99,
        'class'      : 90
    ]
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
// tag::repositories[]
allprojects {

    group 'uk.gov.hmcts.ccd.definition.designer'
    version '1.2.0-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url  "https://dl.bintray.com/hmcts/hmcts-maven"
        }
    }

    apply plugin: 'java'

    ext {
        hibernateVersion = '5.2.16.Final'
    }

    dependencies {
        testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }

    checkstyle {
        maxWarnings = 0
        toolVersion = '8.18'
        configDir = new File(rootDir, 'config/checkstyle')
    }

    pmd {
        toolVersion = '6.11.0'
    }

    checkstyleMain.shouldRunAfter(compileJava)
    test.shouldRunAfter(checkstyleTest)
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// end::repositories[]

subprojects {
    version = project.version

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: "com.github.spotbugs"
    apply plugin: "com.github.kt3k.coveralls"
    apply plugin: 'io.franzbecker.gradle-lombok'


    configurations {
        providedRuntime

        aatCompile.extendsFrom(testCompile)
        aatRuntime.extendsFrom(testRuntime)
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    dependencies {
        compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: reformLogging
        compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: reformLogging
        compile "org.hibernate:hibernate-core:${hibernateVersion}"
        compile "org.hibernate:hibernate-entitymanager:${hibernateVersion}"
        compile (group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.3') {
            // TODO remove when auth-checker-lib upgrades its dependency to spring boot 2.0.x
            exclude group: 'uk.gov.hmcts.reform', module: 'java-logging-spring'
        }
        compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.4'
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-data-jpa') {
            exclude group: 'org.hibernate', module: 'hibernate-core'
            exclude group: 'org.hibernate', module: 'hibernate-entitymanager'
        }
        compile group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
        compile group: 'org.jooq', name: 'jool-java-8', version: '0.9.14'
        compile group: 'javax.inject', name: 'javax.inject', version: '1'

        compile group: 'com.microsoft.azure', name: 'azure-storage', version: '8.0.0'

        // To avoid compiler warnings about @API annotations in JUnit5 code.
        testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'

        testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.21.0'
        testCompile group: 'com.opentable.components', name: 'otj-pg-embedded', version: '0.13.1'
        testCompile "org.hamcrest:hamcrest-core:${hamcrestVersion}"
        testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
        testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
        testRuntime "org.junit.vintage:junit-vintage-engine:${junitVintageVersion}"
        testCompile "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation('org.springframework.cloud:spring-cloud-starter-contract-stub-runner:2.1.0.RELEASE')
        testCompile('org.springframework.cloud:spring-cloud-contract-wiremock:2.1.0.RELEASE')
        testCompile ("org.mockito:mockito-core:2.24.0") {
            exclude group: 'org.hamcrest'
        }
        testCompile "org.mockito:mockito-all:1.10.19"
        testCompile 'org.mockito:mockito-junit-jupiter:2.27.0'
        testCompile 'com.github.npathai:hamcrest-optional:2.0.0'
        testCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '2.1.0.RELEASE'
        testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
        testCompile 'io.github.openfeign:feign-jackson:9.7.0'
        testCompile 'io.github.openfeign.form:feign-form:3.5.0'
        testCompile "org.exparity:hamcrest-date:2.0.5"
        testCompile "io.rest-assured:rest-assured:3.3.0"
    }

    // from https://github.com/junit-team/junit5/issues/1024
    afterEvaluate {
        jacocoTestReport {
            reports {
                // XML required by coveralls and for the below coverage checks
                // and html are generated by default
                xml.enabled true
                html.enabled true
            }

            doLast {
                def report = file("${jacoco.reportsDir}/test/jacocoTestReport.xml")
                logger.lifecycle("Checking coverage results: ${report}")

                def parser = new XmlParser()
                parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
                parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
                def results = parser.parse(report)

                def percentage = {
                    def covered = it.'@covered' as Double
                    def missed = it.'@missed' as Double
                    ((covered / (covered + missed)) * 100).round(2)
                }

                def counters = results.counter
                def metrics = [:]
                metrics << [
                        'class'      : percentage(counters.find { it.'@type'.equals('CLASS') })
                ]


                def failures = []
                metrics.each {
                    def limit = limits[it.key]
                    if (it.value < limit) {
                        failures.add("- ${it.key} coverage rate is: ${it.value}%, minimum is ${limit}%")
                    }
                }

                if (failures) {
                    logger.quiet("------------------ Code Coverage Failed -----------------------")
                    failures.each {
                        logger.quiet(it)
                    }
                    logger.quiet("---------------------------------------------------------------")
                    throw new GradleException("Code coverage failed")
                } else{
                    logger.quiet("Passed Code Coverage Checks")
                }
            }
        }
    }

    test {
        environment("APPINSIGHTS_INSTRUMENTATIONKEY", "some-key")

        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }

        reports {
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport

    checkstyle {
        maxWarnings = 0
        toolVersion = '8.18'
        configDir = new File(rootDir, 'config/checkstyle')
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sonarqube {
    properties {
        property "sonar.junit.reportPaths", 'build/test-results/junit-platform'
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/junitPlatformTest.exec"
        property "sonar.exclusions", "**/entity/*.java,**/model/*.java,**/repository/ClassUserType.java,**/repository/GeneralImmutableJsonType.java,**/AbstractDefinitionRepositoryDecorator.java,**/data/*Data.java,**/ControllerExceptionHandler.java,**/GenericLayoutEntityValidatorImpl.java,**/JurisdictionServiceImpl.java"
        property "sonar.cpd.exclusions", "**/entity/*.java,**/model/*.java,**/repository/ClassUserType.java,**/repository/GeneralImmutableJsonType.java,**/*Application.java,**/HikariConfigurationPropertiesReportEndpoint.java,**/IdamHttpClientConfiguration.java"
        property "sonar.jacoco.excludes", "**/entity/*.java,**/model/*.java,**/repository/ClassUserType.java,**/repository/GeneralImmutableJsonType.java,**/AbstractDefinitionRepositoryDecorator.java,**/data/*Data.java,**/*Controller.java,**/WorkBasketUserDefaultService.java,**/*Exception.java,**/ControllerExceptionHandler.java,**/GenericLayoutEntityValidatorImpl.java,**/JurisdictionServiceImpl.java,**/*Application.java,**/HikariConfigurationPropertiesReportEndpoint.java,**/IdamHttpClientConfiguration.java"
        property "sonar.coverage.exclusions", "**/entity/*.java,**/model/*.java,**/repository/ClassUserType.java,**/repository/GeneralImmutableJsonType.java,**/AbstractDefinitionRepositoryDecorator.java,**/data/*Data.java,**/*Controller.java,**/WorkBasketUserDefaultService.java,**/*Exception.java,**/CaseTypeItem.java,**/ApplicationParams.java,**/CaseTypePresentationControllerService.java,**/DisplayService.java,**/DisplayItemAdapterService.java,**/DisplayApiPresentationControllerService.java,**/ControllerExceptionHandler.java,**/GenericLayoutEntityValidatorImpl.java,**/JurisdictionServiceImpl.java,**/*Application.java,**/HikariConfigurationPropertiesReportEndpoint.java,**/IdamHttpClientConfiguration.java"
        property "sonar.projectName", "ccd-definition-designer-api"
        property "sonar.projectKey", "ccd-definition-designer-api"
    }
}

idea {
    project {
        ipr {
            withXml { provider ->
                // Get XML as groovy.util.Node to work with.
                def projectXml = provider.asNode()
                // Find compiler configuration component.
                def compilerConfiguration = projectXml.component.find { component ->
                    component.'@name' == 'CompilerConfiguration'
                }
                // Replace current annotationProcessing
                // that is part of the compiler configuration.
                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
                currentAnnotationProcessing.replaceNode {
                    annotationProcessing {
                        profile(name: 'Default', default: true, enabled: true) {
                            processorPath(useClasspath: true)
                        }
                    }
                }
            }
        }
    }
}

// this allows ./gradlew run
bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
    archiveName = 'case-definition-designer-api.jar'
}

// Although there are no sources to compile, Jenkins pipeline needs the jar file to be here
dependencies {
    compile "org.hibernate:hibernate-core:${hibernateVersion}"
    compile "org.hibernate:hibernate-entitymanager:${hibernateVersion}"
    compile project(':application')
}

task smoke(type: Test, dependsOn: ":aat:smoke")

task functional(type: Test, dependsOn: ":aat:functional")

task codeCoverageReport(type: JacocoReport, dependsOn: allprojects*.test) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    allprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        csv.enabled = false
        html.enabled = true
        html.destination = file("${buildDir}/reports/jacoco/html")
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
    }
}

// for cnp pipeline
task dependencyCheck(dependsOn: dependencyCheckAnalyze)

configurations { codacy }
repositories {
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}
dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}
task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/jacocoTestReport.xml"
    ]
}
